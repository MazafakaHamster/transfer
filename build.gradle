plugins {
    id 'java'
    id 'idea'
    id 'application'
}

group 'com.rebel'
version '1.0-SNAPSHOT'

sourceCompatibility = 12

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

application {
    mainClassName = 'com.rebel.transfer.Launcher'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestImplementation.extendsFrom testImplementation
}

task integrationTest(type: Test, group: "verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

test {
    useJUnitPlatform()
}

integrationTest {
    useJUnitPlatform()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    compile group: 'io.netty', name: 'netty-all', version: '4.1.35.Final'
    compile group: 'com.hazelcast', name: 'hazelcast', version: '3.12'
    compile group: 'org.json', name: 'json', version: '20180813'
    compile group: 'com.typesafe', name: 'config', version: '1.3.4'
    compile group: 'com.google.guava', name: 'guava', version: '27.1-jre'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.4.2'
    integrationTestCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.8'
    integrationTestCompileOnly 'org.projectlombok:lombok:1.18.6'
}
